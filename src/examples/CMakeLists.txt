macro(ADD_EXAMPLE_BINARY file)
    if (ANDROID)
        add_library(${TARGET_NAME} SHARED ${EXAMPLE})
        add_dependencies(${TARGET_NAME} app-glue)
        target_link_libraries(${TARGET_NAME} app-glue)
    else()
        add_executable(${TARGET_NAME} ${EXAMPLE})
    endif()
    set_target_properties(${TARGET_NAME} PROPERTIES FOLDER "examples")
endmacro()


macro(BUILD_EXAMPLE EXAMPLE)
    get_filename_component(TARGET_NAME ${EXAMPLE} NAME_WE)
    get_filename_component(TARGET_FOLDER ${EXAMPLE} DIRECTORY)
    add_example_binary(${EXAMPLE})
    target_link_libraries(${TARGET_NAME} PUBLIC vks_examples vks_vks)
    target_link_libraries(${TARGET_NAME} ${CMAKE_THREAD_LIBS_INIT})

    unset(SHADER_FILES)
    while(NOT ("${TARGET_FOLDER}" STREQUAL ""))
        if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/${TARGET_FOLDER}/custom.cmake")
            include("${CMAKE_CURRENT_LIST_DIR}/${TARGET_FOLDER}/custom.cmake")
        endif()
        get_filename_component(TARGET_FOLDER ${TARGET_FOLDER} DIRECTORY)
    endwhile()

    # If the custom.cmake didn't specify shaders for a project, try to find them based on the target_name
    if (NOT SHADER_FILES)
        find_target_shaders()
    endif()

    if (SHADER_FILES)
        target_shader_files()
    endif()
endmacro()

file(GLOB_RECURSE EXAMPLES RELATIVE ${CMAKE_CURRENT_LIST_DIR} *.cpp)
message(STATUS "Adding examples ${EXAMPLES}")
foreach(EXAMPLE ${EXAMPLES})
    build_example(${EXAMPLE})
endforeach()
